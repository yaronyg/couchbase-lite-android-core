
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

sourceCompatibility = 1.6

repositories {
    mavenLocal()
    maven { url "http://thaliartifactory.cloudapp.net/artifactory/libs-snapshot" }
    maven { url "http://thaliartifactory.cloudapp.net/artifactory/libs-release" }
    maven { url 'http://files.couchbase.com/maven2/' }
    mavenCentral()
}

dependencies {

    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.2'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
    // Using this version of apache http client based on: http://bit.ly/1kiET7V
    // Decided to use jars instead so this wasn't accidentally included in android version
    // compile 'org.apache.httpcomponents:httpcore:4.0-beta2'  
    // compile 'org.apache.httpcomponents:httpclient:4.0-beta1'  

    compile fileTree(dir: 'libs', include: '*.jar')
}


task createMavenDirectory(type: Exec) {

    ext {
        uploadUser = System.getenv("MAVEN_UPLOAD_USERNAME") + ":" + System.getenv("MAVEN_UPLOAD_PASSWORD")
        mkcolPath  = System.getenv("MAVEN_UPLOAD_REPO_URL") + "com/couchbase/lite/couchbase-lite-java-core/" + System.getenv("MAVEN_UPLOAD_VERSION") + "/"
    }
    commandLine "curl", "--user", uploadUser, "-X", "MKCOL", mkcolPath
}

// this hack is only needed for apache mod_dav based Maven repo's like file.couchbase.com.  otherwise, skip it
createMavenDirectory.onlyIf { System.getenv("MAVEN_UPLOAD_REPO_URL").contains("files") }

// first create the directory, then do the upload
task uploadArchivesWrapper(dependsOn: createMavenDirectory) << {
    uploadArchives.execute()
}

// https://github.com/couchbase/couchbase-lite-java-core/issues/33
def mavenPath() {
    artifactory_local == "true" ?
            'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath :
            artifactory_contextUrl + "/libs-release-local"
}

// this will upload, but will not first create a directory (which is needed on some servers)
uploadArchives {
    repositories {
        mavenDeployer {
			// https://github.com/couchbase/couchbase-lite-java-core/issues/33
            repository(url: mavenPath()) {
                authentication(userName: "${artifactory_user}", password: "${artifactory_password}")
            //repository(url: System.getenv("MAVEN_UPLOAD_REPO_URL")) {
            //    authentication(userName: System.getenv("MAVEN_UPLOAD_USERNAME"), password: System.getenv("MAVEN_UPLOAD_PASSWORD"))
            }
            pom.version = "${System.properties['UPLOAD_VERSION_CBLITE']}"  // https://github.com/couchbase/couchbase-lite-java-core/issues/34
            //pom.version = System.getenv("MAVEN_UPLOAD_VERSION")
            pom.groupId = 'com.couchbase.lite'
            pom.artifactId = 'couchbase-lite-java-core'
            pom.project {
                licenses {
                    license {
                        name 'Couchbase Community Edition License Agreement'
                        url 'http://www.couchbase.com/agreement/community'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

task generateJavadocs(type: Javadoc) {
    source = sourceSets.main.java.srcDirs
    List<File> pathList = new ArrayList<File>();
    pathList.add(file('extra/doclet/doclet.jar'))
    options.docletpath = pathList
    options.doclet = "ExcludeDoclet"
    options.showFromPublic()
    exclude "org/apache/http/**", "com/couchbase/touchdb/**"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}
